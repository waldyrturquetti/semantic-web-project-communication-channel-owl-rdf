# 1 -> ALL COMMUNICATION CHANNEL PREFERENCE WEIGHT BY USER

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.semanticweb.org/1513mxti/ontologies/2023/11/CCOntology#>
PREFIX cco2: <http://www.semanticweb.org/1513mxti/ontologies/2024/0/CCOntology#>

SELECT ?user_name ?communication_resource_value ?communication_resource_type ?communication_channel_type ?preference_weight
WHERE {
    ?user rdf:type cco:User;
    cco:Name ?user_name .

    ?communication_resource rdf:type cco:Communication_Resource;
    cco:Type ?communication_resource_type .

    ?communication_resource cco:Preference_Weight ?preference_weight .
    ?communication_resource cco2:Value ?communication_resource_value .

    ?user cco:HAVE ?communication_resource .
    ?communication_resource cco:USE ?communication_channel .
    ?communication_channel cco:Type ?communication_channel_type .

    FILTER (?user_name = "$name"^^xsd:string)
}
ORDER BY DESC (?preference_weight)

--------------------------------------------------------------------------

# 2 -> BEST COMMUNICATION CHANNEL BY USER

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.semanticweb.org/1513mxti/ontologies/2023/11/CCOntology#>
PREFIX cco2: <http://www.semanticweb.org/1513mxti/ontologies/2024/0/CCOntology#>

SELECT ?user_name ?communication_resource_value ?communication_resource_type ?communication_channel_type ?preference_weight
WHERE {
    ?user rdf:type cco:User;
        cco:Name ?user_name .
    ?communication_resource rdf:type cco:Communication_Resource;
        cco:Type ?communication_resource_type ;
        cco:Preference_Weight ?preference_weight .
    ?communication_resource cco2:Value ?communication_resource_value .
    ?user cco:HAVE ?communication_resource;
    {
        SELECT (MAX(?preference_weight) AS ?max_preference)
        WHERE {
            ?user rdf:type cco:User;
                cco:Name "$name"^^xsd:string ;
                cco:HAVE ?communication_resource .
            ?communication_resource rdf:type cco:Communication_Resource;
                cco:Preference_Weight ?preference_weight .
        }
    }
    ?communication_resource cco:USE ?communication_channel .
    ?communication_channel cco:Type ?communication_channel_type
    FILTER (?user_name = "$name"^^xsd:string && ?preference_weight = ?max_preference)
}
ORDER BY DESC (?preference_weight)

--------------------------------------------------------------------------

# 3 -> THE COUNT OF ACTIONS DONE FOR EACH TIME OF DAY BY USER

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.semanticweb.org/1513mxti/ontologies/2023/11/CCOntology#>

SELECT ?name ?timeOfDay (COUNT(?action) as ?actionCount)
WHERE {
   ?user rdf:type cco:User .
   ?action rdf:type cco:Actions .

   ?user cco:Name ?name .
   ?user cco:PERFORM_ACTION ?action .
   ?action cco:Date_and_Time ?actionDateTime .

   # Extracting the hour component
   BIND(HOURS(?actionDateTime) as ?hour) .

   # Categorizing actions based on time of day
   BIND(
      IF(?hour >= 6 && ?hour < 12, "Morning",
         IF(?hour >= 12 && ?hour < 18, "Afternoon",
            IF(?hour >= 18 && ?hour < 24, "Evening",
               IF(?hour >= 0 && ?hour < 6, "Night", "Undefined")
            )
         )
      ) as ?timeOfDay
   )
   FILTER (regex(?name, "$name", "i"))
}
GROUP BY ?name ?timeOfDay

--------------------------------------------------------------------------

# 4 -> BY EACH ACTION CATEGORY GET THE COUNT OF ACTION DONE BY USER

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.semanticweb.org/1513mxti/ontologies/2023/11/CCOntology#>

SELECT ?name ?actionCategory (COUNT(?actionCategory) as ?actionCategoryCount)
WHERE {
   ?user rdf:type cco:User .
   ?action rdf:type cco:Actions .

   ?user cco:Name ?name .
   ?user cco:PERFORM_ACTION ?action .
   ?action cco:Action_Category ?actionCategory .

   FILTER (regex(?name, "$name", "i"))
}
GROUP BY ?name ?actionCategory

--------------------------------------------------------------------------

# 5 -> BY EACH ACTION CATEGORY GET THE GREATER COUNTS OF ACTION DONE BY USER

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cco: <http://www.semanticweb.org/1513mxti/ontologies/2023/11/CCOntology#>

SELECT ?name ?actionCategory ?actionCategoryCount
WHERE {
  {
    SELECT ?name ?actionCategory (COUNT(?actionCategory) as ?actionCategoryCount)
    WHERE {
      ?user rdf:type cco:User .
      ?action rdf:type cco:Actions .
      ?user cco:Name ?name .
      ?user cco:PERFORM_ACTION ?action .
      ?action cco:Action_Category ?actionCategory .
      FILTER (regex(?name, "$name", "i"))
    }
    GROUP BY ?name ?actionCategory
  }

  {
    SELECT ?name (MAX(?actionCategoryCount) as ?maxCount)
    WHERE {
      {
        SELECT ?name ?actionCategory (COUNT(?actionCategory) as ?actionCategoryCount)
        WHERE {
          ?user rdf:type cco:User .
          ?action rdf:type cco:Actions .
          ?user cco:Name ?name .
          ?user cco:PERFORM_ACTION ?action .
          ?action cco:Action_Category ?actionCategory .
          FILTER (regex(?name, "$name", "i"))
        }
        GROUP BY ?name ?actionCategory
      }
    }
    GROUP BY ?name
  }

  FILTER (?actionCategoryCount = ?maxCount)
}